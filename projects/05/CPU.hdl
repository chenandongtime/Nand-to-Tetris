// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:

    // if it is A instruction,load ARegister with instruction else  judge C instruction whether need load ALUOut to ARegister
    Not(in = instruction[15],out = Ains);
    Mux16(a = outALU, b = instruction, sel = Ains, out = inAR);
    Or(a = Ains,b=instruction[5],out = loadAR); 
    ARegister(in = inAR,load = loadAR ,out = AR ,out[0..14] = addressM);
    
    //if it is C instruction
    Not(in = Ains,out = Cins);

   // 如果是C指令并且规定写入到M
   And(a = Cins, b = instruction[3], out = writeM);

   // decide use inM or A value
   Mux16(a = AR,b = inM,sel = instruction[12],out = AM);

     //calculate ALU 
    And(a = instruction[4], b = Cins, out = isLoadDR);
    DRegister(in = outALU,load = isLoadDR,out = DR);

    // ALU(x = DR,y=AM,zx = instruction[11],nx = instruction[10],zy=instruction[9],ny = instruction[8],f = instruction[7],no=instruction[6],out = outALU,zr = zr,ng=ng)
    And(a = Cins, b = instruction[6], out = no);
    And(a = Cins, b = instruction[7], out = f);
    And(a = Cins, b = instruction[8], out = ny);
    And(a = Cins, b = instruction[9], out = zy);
    And(a = Cins, b = instruction[10], out = nx);
    And(a = Cins, b = instruction[11], out = zx);

    ALU(x = DR, y = AM, zx = zx, nx = nx, zy = zy, ny = ny, f = f, no = no, out = outALU, out = outM, zr=zr, ng=ng);
   
   // 根据j位域和ALU的zr、ng位来判断跳转
    And(a = Cins, b = instruction[0], out = isGT);
    And(a = Cins, b = instruction[1], out = isEQ);
    And(a = Cins, b = instruction[2], out = isLT);

    And(a = ng, b = isLT, out = isLtJump);
    And(a = zr, b = isEQ, out = isEqJump);

    // 输出是否大于0
    Not(in = ng, out = notNg);
    Not(in = zr, out = notZr);
    And(a = notNg, b = notZr, out = isOutGt);
    
    And(a = isOutGt, b = isGT, out = isGtJump);

    Or(a = isLtJump, b = isEqJump, out = isJump);
    Or(a = isJump, b = isGtJump, out = jump);

    PC(in = AR, load = jump, inc = true, reset = reset, out[0..14] = pc);
}